plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://maven.juspay.in/hyper-sdk'
    }
}

dependencies {
    implementation gradleApi()

    // Add the Android Gradle Plugin dependency
    implementation 'com.android.tools.build:gradle:8.5.0'
    
    // Test dependencies
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.13.2'
}

gradlePlugin {
    plugins {
        hyperPlugin {
            id = 'io.hyperswitch.plugin'
            implementationClass = 'io.hyperswitch.HyperPlugin'
        }
        hyperSettingsPlugin {
            id = 'io.hyperswitch.settings.plugin'
            implementationClass = 'io.hyperswitch.HyperSettingsPlugin'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group = 'io.hyperswitch'
version = '0.2.3'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from tasks.javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Hyperswitch SDK for Android'
                description = 'This is the Hyperswitch SDK for Android, providing seamless integration with the Hyperswitch platform.'
                url = 'http://www.hyperswitch.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/hyperswitch/hyperswitch-sdk-android'
                    connection = 'scm:git:git://github.com/juspay/hyperswitch-sdk-android.git'
                    developerConnection = 'scm:git:ssh://git@github.com:juspay/hyperswitch-sdk-android.git'
                    tag = 'HEAD'
                }
                developers {
                    developer {
                        id = 'sh-iv-am'
                        name = 'Shivam'
                        email = 'shivam.shashank@juspay.in'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = uri('../maven')
        }
    }
}

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
        classpath("io.hyperswitch:react-native-gradle-plugin:0.75.2.1")
    }
}

apply plugin: "io.hyperswitch.rootproject"
apply plugin: "maven-publish"

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url("$rootDir/maven/")
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url("$rootDir/../node_modules/detox/Detox-android")
        }
        maven {
            url "https://maven.juspay.in/hyper-sdk/"
        }
    }
}

def runtimeDeps = [
        'react-native-inappbrowser-reborn',
        'react-native-svg',
        'sentry_react-native',
]

def runtimeDepsExcluded = [
        'demo-app',
        'demo-app-lite'
]

def runtimeDepsOptional = [
        'react-native-hyperswitch-kount',
        'react-native-hyperswitch-paypal',
        'react-native-hyperswitch-netcetera-3ds',
        'react-native-hyperswitch-samsung-pay',
        'react-native-hyperswitch-scancard',
        'react-native-klarna-inapp-sdk',
]

def configurePomMetadata = { root ->
    root.appendNode('name', 'Hyperswitch SDK for Android')
    root.appendNode('description', 'This is the Hyperswitch SDK for Android, providing seamless integration with the Hyperswitch platform.')
    root.appendNode('url', 'http://www.hyperswitch.io')

    def licenseNode = root.appendNode('licenses').appendNode('license')
    licenseNode.appendNode('name', 'The Apache License, Version 2.0')
    licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
    licenseNode.appendNode('distribution', 'repo')

    def scmNode = root.appendNode('scm')
    scmNode.appendNode('url', 'https://github.com/hyperswitch/hyperswitch-sdk-android')
    scmNode.appendNode('connection', 'scm:git:git://github.com/juspay/hyperswitch-sdk-android.git')
    scmNode.appendNode('developerConnection', 'scm:git:ssh://git@github.com:juspay/hyperswitch-sdk-android.git')
    scmNode.appendNode('tag', 'HEAD')

    def developerNode = root.appendNode('developers').appendNode('developer')
    developerNode.appendNode('id', 'sh-iv-am')
    developerNode.appendNode('name', 'Shivam')
    developerNode.appendNode('email', 'shivam.shashank@juspay.in')
}

subprojects {

    // def disableTaskIfPresent = { taskPath ->
    //     def task = tasks.findByPath(taskPath)
    //     if (task) task.enabled = false
    // }

    // disableTaskIfPresent(':react-native-klarna-inapp-sdk:compileDebugUnitTestJavaWithJavac')
    // disableTaskIfPresent(':react-native-klarna-inapp-sdk:compileReleaseUnitTestJavaWithJavac')
    // disableTaskIfPresent(':react-native-klarna-inapp-sdk:testReleaseUnitTest')
    // disableTaskIfPresent(':react-native-hyperswitch-paypal:lintDebug')

    def isAppProject = project.name == 'app'
    def isLiteProject = project.name == 'hyperswitch-sdk-android-lite'
    def pomVersion = isAppProject ? project.version : (isLiteProject ? liteSdkVersion : libsVersion)

    def configurePom = { pom ->
        def root = pom.asNode()
        configurePomMetadata(root)

        def dependencies = root.appendNode('dependencies')
        configurations.getByName('releaseCompileClasspath').getResolvedConfiguration().getFirstLevelModuleDependencies().each {
            if (!runtimeDepsOptional.contains(it.moduleName)) {
                def dependency = dependencies.appendNode('dependency')
                dependency.appendNode('groupId', it.moduleGroup)
                dependency.appendNode('artifactId', it.moduleName)
                dependency.appendNode('version', (runtimeDeps.contains(it.moduleName) ? extVersion : it.moduleVersion))
            }
        }
    }

    publishing {
        publications {
            if (!runtimeDepsExcluded.contains(project.name)) {
                "$project.name"(MavenPublication) {
                    groupId = project.group
                    artifactId = isAppProject ? 'hyperswitch-sdk-android' : project.name
                    version = pomVersion
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    pom.withXml(configurePom)
                }
            }
        }
        repositories {
            maven {
                url("${project.rootDir}/maven")
            }
        }
    }
}
